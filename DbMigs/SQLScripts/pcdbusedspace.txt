 DECLARE @DriveSpaceFree TABLE (Drive CHAR(1),FreeMB INT);
 
INSERT   INTO @DriveSpaceFree
         EXEC xp_fixeddrives
 
SELECT
   (SUM(SpaceUsedInFiles) / SUM(TotalAvailableSpaceForFiles)) * 100 AS PctFilled
FROM
   (SELECT
      Drive
     ,SUM(FileSpaceUsed) AS SpaceUsedInFiles
     ,CASE WHEN MIN(FileMaxSize) = -1 -- unlimited growth => space available is limited by free space on drive
                OR (MIN(FileMaxSize) <> -1 -- limited growth, but less space on drive than growth space left in files => space available is limited by free space on drive
                    AND MAX(FreeOnDrive) < SUM(FileMaxSize - CurrentFileSize)) THEN SUM(CurrentFileSize) + MAX(FreeOnDrive)
           ELSE SUM(FileMaxSize)
      END AS TotalAvailableSpaceForFiles
    FROM
      (SELECT
         FILEPROPERTY(df.name,'SpaceUsed') / 128. AS FileSpaceUsed
        ,df.size / 128. AS CurrentFileSize
        ,CASE WHEN df.max_size > 0 THEN df.max_size / 128.
              WHEN df.max_size < 0 THEN -1 -- unlimited growth
              ELSE df.size / 128. -- max_size=0 => no growth is allowed => FileMaxSize = CurrentFileSize
         END AS FileMaxSize
        ,dsf.Drive
        ,dsf.FreeMB AS FreeOnDrive
       FROM
         sys.database_files df
         JOIN @DriveSpaceFree dsf
            ON LEFT(df.physical_name,1) = dsf.Drive
       WHERE
         df.type = 0 /* rows data */) AS InfoPerFile
    GROUP BY
      Drive) AS InfoPerDrive